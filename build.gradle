plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'io.freefair.lombok' version '5.1.0'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
    id 'jacoco'
    id "org.sonarqube" version "3.0"
}

group 'com.vitech'
version '1.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

testSets {
    integrationTest
}

compileJava {
    options.compilerArgs << "-Werror" << "-Xlint:all"
}

tasks.withType(Test) {
    useJUnitPlatform()   // use JUnit 5
    maxParallelForks = 1 // increase this value to run tests in parallel
}

def checkTestsExecutionTime = { TestDescriptor descriptor, TestResult result, String timeLimitProperty ->
    if (descriptor.parent == null) {
        def limit = project.findProperty(timeLimitProperty) as Integer
        def execTime = (result.endTime - result.startTime) / 1000
        if (execTime > limit) {
            throw new GradleException("Tests execution time [${execTime}sec] exceeded configured limit [${limit}sec]")
        }
    }
}

test {
    useJUnitPlatform()   // use JUnit 5
    afterSuite { desc, res -> checkTestsExecutionTime(desc, res, "tests.small.execution.time.limit.seconds") }
    finalizedBy jacocoTestReport
    ignoreFailures = true
}

integrationTest {
    afterSuite { desc, res -> checkTestsExecutionTime(desc, res, "tests.medium.execution.time.limit.seconds") }
    finalizedBy jacocoIntegrationTestReport
}

tasks.withType(JacocoReport) {
    reports {
        html.enabled = true
        xml.enabled true
    }
}

bootJar {
    layered()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.liquibase:liquibase-core:3.10.2'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'org.modelmapper:modelmapper:2.3.8'
    runtimeOnly 'mysql:mysql-connector-java'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.testcontainers:mysql:1.14.3"
    testImplementation 'com.tngtech.archunit:archunit-junit5:0.14.1'
}
